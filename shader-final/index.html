<!DOCTYPE html>
<html lang="en">

<head>
	<title>Shader-The Great Wave off Kanagawa and Dwelling in the Fuchun Mountains</title>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<link rel="stylesheet" href="style.css">
	<script src="https://unpkg.com/three@0.106.0/build/three.min.js"></script>
	<script src="https://unpkg.com/three@0.106.0/examples/js/controls/OrbitControls.js"></script>
	<script src='https://cdnjs.cloudflare.com/ajax/libs/jquery/2.1.3/jquery.min.js'></script>

	<style>
		
	</style>

</head>

<body>
	<div>
	<audio id="audio1" src="assets/audio1.mp3"></audio>
    <audio id="audio2" src="assets/audio2.mp3"></audio>
	</div>

	<script id="texture_vertex_simulation_shader" type="x-shader/x-vertex">

        varying vec2 vUv;
        varying vec3 vOffset;

        uniform vec3 offset;
        uniform mat4 inverseModelViewMatrix;

        void main() {

            vOffset = ( inverseModelViewMatrix * vec4( offset, 1. ) ).xyz;
            vUv = vec2(uv.x, 1.0 - uv.y);
            gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

        }

    </script>

	<script id="texture_fragment_simulation_shader" type="x-shader/x-fragment">



    vec3 mod289(vec3 x_) { return x_ - floor(x_ * (1.0 / 289.0)) * 289.0;  }

    vec4 mod289(vec4 x_) { return x_ - floor(x_ * (1.0 / 289.0)) * 289.0; }

    vec4 permute(vec4 x_) { return mod289(((x_ * 34.0) + 1.0) * x_); }

    vec4 taylorInvSqrt(vec4 r_){ return 1.79284291400159 - 0.85373472095314 * r_; }

    float snoise(vec3 v_) {

        const vec2  C = vec2(1.0 / 6.0, 1.0 / 3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        vec3 i = floor(v_ + dot(v_, C.yyy) );
        vec3 x0 = v_ - i + dot(i, C.xxx);

        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy;
        vec3 x3 = x0 - D.yyy;

        i = mod289(i);
        
        vec4 p = permute( permute( permute(
              i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
            + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
            + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        float n_ = 0.142857142857;
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        vec4 s0 = floor(b0) * 2.0 + 1.0;
        vec4 s1 = floor(b1) * 2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww ;

        vec3 p0 = vec3(a0.xy, h.x);
        vec3 p1 = vec3(a0.zw, h.y);
        vec3 p2 = vec3(a1.xy, h.z);
        vec3 p3 = vec3(a1.zw, h.w);

        vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));

    }

    vec3 snoiseVec3( vec3 x_ ){

        float s  = snoise(vec3( x_ ));
        float s1 = snoise(vec3( x_.y - 19.1 , x_.z + 33.4 , x_.x + 47.2 ));
        float s2 = snoise(vec3( x_.z + 74.2 , x_.x - 124.5 , x_.y + 99.4 ));
        vec3 c = vec3( s , s1 , s2 );
        return c;

    }

    vec3 curlNoise( vec3 p_ ){

        const float e = .1;
        vec3 dx = vec3( e   , 0.0 , 0.0 );
        vec3 dy = vec3( 0.0 , e   , 0.0 );
        vec3 dz = vec3( 0.0 , 0.0 , e   );

        vec3 p_x0 = snoiseVec3( p_ - dx );
        vec3 p_x1 = snoiseVec3( p_ + dx );
        vec3 p_y0 = snoiseVec3( p_ - dy );
        vec3 p_y1 = snoiseVec3( p_ + dy );
        vec3 p_z0 = snoiseVec3( p_ - dz );
        vec3 p_z1 = snoiseVec3( p_ + dz );

        float x = p_y1.z - p_y0.z - p_z1.y + p_z0.y;
        float y = p_z1.x - p_z0.x - p_x1.z + p_x0.z;
        float z = p_x1.y - p_x0.y - p_y1.x + p_y0.x;

        const float divisor = 1.0 / ( 2.0 * e );
        return normalize( vec3( x , y , z ) * divisor );

    }

    varying vec2 vUv;
    varying vec3 vOffset;

    uniform float active;
    uniform sampler2D tPositions;
    uniform sampler2D oPositions;
    uniform float width;
    uniform float height;
    uniform vec3 offset;

    uniform float timer;
    uniform float delta;
    uniform float speed;
    uniform float reset;
    uniform float genScale;
    uniform float factor;
    uniform float evolution;
    uniform float radius;
    
    mat4 rotationMatrix(vec3 axis_, float angle_) {

        axis_ = normalize(axis_);
        float s = sin(angle_);
        float c = cos(angle_);
        float oc = 1.0 - c;

        return mat4(oc * axis_.x * axis_.x + c, oc * axis_.x * axis_.y - axis_.z * s, oc * axis_.z * axis_.x + axis_.y * s, 0.0,
                    oc * axis_.x * axis_.y + axis_.z * s, oc * axis_.y * axis_.y + c, oc * axis_.y * axis_.z - axis_.x * s, 0.0,
                    oc * axis_.z * axis_.x - axis_.y * s,  oc * axis_.y * axis_.z + axis_.x * s,  oc * axis_.z * axis_.z + c, 0.0,
                    0.0, 0.0, 0.0, 1.0);
                    
    }
    
    void main() {

        vec4 c = texture2D( tPositions, vUv );
        vec3 pos = c.xyz;
        float life = c.a;

        float s = vUv.x * life / 100.;
        float speedInc = 1.;
        if( s > .95 ) speedInc = .75;
        else if( s > .9 ) speedInc = .85;
        else speedInc = 1.;

        vec3 v = factor * speedInc * delta * speed * ( curlNoise( .2 * pos + factor * evolution * .1 * timer ));
        pos += v;
        life -= factor * 1.;

        if( length( pos ) < radius ) {  pos = normalize( pos ) * radius; }

        if( life <= 0. || reset == 1. ) {

            pos = ( rotationMatrix( vec3( 1., 0., 0. ), timer ) * texture2D( oPositions, vUv ) ).xyz + vOffset;
            life = 100.;

        }

        gl_FragColor = vec4( pos, life );

    }


    </script>

	<script type='x-shader/x-vertex' id='vs-points'>

		attribute vec3 color;
		attribute vec3 color2;
		attribute vec2 inUV;
		varying float vOffset;
		
		
        uniform float size;
		uniform float scale;
		uniform float offset;
        uniform sampler2D updatedPosition;

		varying vec3 vColor;
		varying vec3 vColor2;

        void main() {

			vColor = color;
			vColor2 = color2;
            vec3 pos = texture2D(updatedPosition, inUV).xyz;
            vec4 mvPosition = modelViewMatrix * vec4( pos, 1.0 );
            gl_PointSize = size * ( scale / -mvPosition.z );
			gl_Position = projectionMatrix * mvPosition;
			vOffset=offset;

        }


    </script>

	<script type='x-shader/x-fragment' id='fs-points'>

		varying vec3 vColor;
		varying vec3 vColor2;
		uniform sampler2D blob;
		//uniform sampler2D blob2;
		varying float vOffset;

        void main() { 
			//console.log("uTime")
			//gl_FragColor = ((vColor*abs(sin(uTime)) + vColor2*(1-abs(sin(uTime)))),1.0);
			gl_FragColor = vec4(mix(vColor, vColor2, vOffset), 1.0);
			//gl_FragColor = vec4(vColor2, 1.0);
			//gl_FragColor = vec4(vColor, 1.0);
			
			gl_FragColor = gl_FragColor * texture2D(blob, gl_PointCoord);
			
            if (gl_FragColor.a < 0.08) discard;

        }


    </script>

	<script src="app.js"></script>
	
	

</body>

</html>